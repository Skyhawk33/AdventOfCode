<vi@#"data/day10.txt"001*aa{0           n(4"FRTH"@#    S0c':*fa"."(4"TOYS"@#                       ;;;;;;;;;;;;
 > 0{2u0}     v       ;init SOSS to contain the size;                                              ;read input;
 v uN2\p:N2:\ <       ;init width;                                                                 ;file here ;
 >                  v                                                                              ;;         
;;;;;;;;;;;;;;;;;;;;  ; 1. find the S ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                                                                                                   ;;
                    v                              <                                               ;;   ;-2,-2=width;
;newline; >\u:2*:g\u>1u:!#v_ 1-:1u:1Nu1-g\ 1Nu 'S- |                                               ;;   ;-2, 0=Sx;   
          |     :+u1::-1  <                                                                        ;;   ;-2, 1=Sy;   
      @,?'< v          ,a p1N2 .: -1 u1 p0N2 .: u1 <                                               ;;   ;-2, 2=current mem column;
                                                                                                   ;;   
;;;;;;;;;;;   ;; 2. init the pos and vel  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;   ;-3,01=pos 0;
            n                                                                                      ;;   ;-3,23=vel 0;
             >3N 2N2p 1 3N4p 1 4N4p              3 0 01 '| 1N0'J 10 'L 01  v                       ;;   ;-3, 4=count 0;
             ^            p5N40 p5N30 <        > 2 0 01N'| 1N0'7 10 'F 01N v                       ;;   ;-4,01=pos 1;
            >  0 'd3*: 2N0g+\ 2N1g+ p ^        > 1 0 10 '- 01N'J 01 '7 10  v                       ;;   ;-4,23=vel 1;
                                               > 0 0 1N0'- 01N'L 01 'F 1N0 v                       ;;   ;-4, 4=count 1;
                                               >    '?,@                                           ;;   
                                               j                                                   ;;   ;-3, 5=rotation 0;
                          >2N2p 0>$            ^                                                   ;;   ;-4, 5=rotation 1;
 >$_ '|,      2N2g1- :5+  |  >RR $$   v                                                            ;;   
                                 |!:\ < g p1 g2N2: +g1N2  \p0 g2N2: +g0N2\ <                       ;;   ;-2, 3=enclosed;
                                                                                                   ;;
                          #  |-O <                                                                 ;;
   ^ p3 g2N2 .: p2 g2N2 .: \$<                                                                     ;;
                                                                                                   ;;
;;;;;;;;;;;;;;;;;;;;;;;;;   ;; 3. move until they collide ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                          n                             >  0 'd3*: 3N0g+\ 3N1g+ p                 v;;   ; - and | are straight ;
          >              n> 3N0g 4N0g-! 3N1g 4N1g-!    *|                                          ;;   ; F and J are / mirrors =(-y,-x);
 > 1-: 5+!|                                             > 3N a,v                                   ;;   ; 7 and L are \ mirrors =(y,x);
          >                                                 '/,>::0g.1g.  ::4g1+\ 4p            v  ;;
  v p3O p2 \  uN1:g3: \  uN1:g2::$ <                                                               ;;
                                   | +!-L' \!-7' ::  g g1 \g0:: < p1O +g3 \g1:: p0O +g2 \g0:: <    ;;
  v p3O p2 \N uN1:g3: \N uN1:g2::$<>                v                                              ;;
  >::2g1u+\3g1u-+:*2/N1+O5g+O5p v |  +!-J' \!-F' :: <               >                         ^    ;;   ;keep track of rotations;
 ^                              <$<                                 ^ p +*3d' g1 \+*3d' g0: \0: <  ;;   ;mark all cells with 0;
             v                                                                                    <;;
                                                                                                   ;;
;;;;;;;;;;;     ;; 4. fill in the loop  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
             n                                                                                     ;;
             >3N5g 4N5g- 0`2*1-: 3N5p N4N5p 02N3p 113Nv                                            ;;
                                                                                                   ;;
                                                        > a, 2N3g. a, @                            ;;
          >              n> 3N0g 4N0g-! 3N1g 4N1g-!    *|                                          ;;
 > 1-: 5+!|                                             > 3N a,v                                   ;;
          >                                         >       '/,>::0g.1g.                v          ;;
    v p3O p2 \ g3: \ g2::  $ <                        #                                            ;;
                             | +!-L' \!-7' ::  g g1 \g0::<v p1O +g3 \g1:: p0O +g2 \g0:: <          ;;
    v p3O p2 \Ng3: \Ng2::  $<>                v     #                     >v                       ;;
                            |  +!-J' \!-F' :: <       >   >>:5g OOO2g*:1-!|>O1g+ 1Nu :3g RN*\ 0g+v ;;
 ^                         $<                       |  \_^#         \$$< $<                        ;;
    >1\                                                    ^           | !gOO < +u1 \+O\ *3d'    < ;;
 ^                                                  <    v +1 pRR 0 OO <                           ;;
                                                         > 2N3g1+ :. 2N3p     ^                    ;;
                                                                                                   ;;
 ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;