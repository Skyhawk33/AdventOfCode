<vi@#"data/day3.txt"001*8a{0            ;(4"INDV"@#;   S0O':*fa"."(4"TOYS"@#   ;;;;;;;;;;;;
 >0{2u0}     v        ;init SOSS to contain the size;                          ;read input;
 vuN2\p:N2:\ <        ;init width;                                             ;file here ;
 >022Np             v ;init acc;                                               ;;         
                        >1-:1u: 1Nu1-g\ 1Nuv  ;read cell;                      ;;   ;-2,-2=width;
;newline; >\u:2*:g\u>1u:|    v`\0:-2[:-".":<  ;filter out '.';                 ;;   ;0-1,-2=y2,x2;
          |     :+u1::-1<   v_9`]v  |           ;filter out 0-9;               ;;   ;2,-2=accumulator;
@,.,agN22a<         #           |    > 032Np v                                 ;;         
                    ^n          <   < vpN241 <  ;init position y2,x2 and count;;;   ;for dy=-2, -1, 0;
                            >$   >2N ^>    >:2u: 1Nu\: 1Nu1- 12Np+ 02Np  v     ;;   ;start at dx=-1;
                    ^n  <     _v#-*'$_     ^  >    12Ng1- 12Np           v     ;;   ;init a new position (x+dx, y+dy);
         >22Ng+22Npn^   |-2gN23<     `        |!+`9\`\0:-0'  ggN20 gN21  <     ;;   ;decrease dx until you hit a non-digit;
         ^       .: gN24<            0        >    12Ng1+ 12Np              0v ;;   ;read digits moving right until you hit dx=0;
                                     :>  :!!32Ng+32Np:!+ 42Ng*42Np012Ng1+12Npv ;;   ;if dx=0 is not a digit, commit what you have;
                                     +|!-uN1:u1gN21$ _v#+`9\`\0::-0'ggN20gN21< ;;   ;  and continue right with a 0;
                                     1>  :!!32Ng+32Nv >\a*+ 12Ng1+ 12Np      ^ ;;   ;otherwise, continue right with what you have;
                                     ^ pN24*gN24+!:p<  ;row=10*row+digit;              
                                      ;acc += row;                                   ;always count the number of numbers;
                                                                                     ;if there were two, and it was *;
                                                                                     ;  add the product to the total;
                                                                                          
                                                                                          
                                                                                          
                                                                                          
                                                                                          
                                                                                          