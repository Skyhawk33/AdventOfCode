<_ZZZZZ    ;;              v p20 p10 p    n(4"STRN"(4"FRTH"  }nu2{{                                  ; vars:                            ;
 _ZZZZZ    ;;                                                                                        ; 00=size, 01=sorted, 02=candidate ;
 _ZZZZZ    ;;;;;;;;;;;;;;   ; 1. convert characters ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 03=strength                      ;
  ...      ;; >g1+ 0:p     >#v~:' -]:'A-]:'K-]:'T-]:'J-]:   v                                        ; x9 y? = strength calculation     ;
           ;;                '     |    |    |    |    |                                             ; x2 y? = hands                    ;
           ;;                a          '    '    '    '                                             ; x-1 y? = bids (offscreen)        ;
           ;;                ,          X    R    F    0                                             ; x1 y? = strengths                ;
           ;;              ^  u-10 $\   <    <    <    <    <                                        ;                                  ;
           ;;                #                                                                       ; 1. we need to convert the hands  ;
           ;; ^ pg00-10& Pg002 u5  <                                                                 ;    so that string comparison can ;
           ;;;;;;;;;;;;;;;;     ; 2. assign strengths ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;    evaluate ties                 ;
           ;;                v                       <    v p\9\0<                                   ;    A=X K=R T=F                   ;
           ;;                > :2\G 003p 0"23456789F0QRX" >     :|                                   ;  p.2: J is weak, J=0             ;
           ;;                 > :9\g :03g+03p 1+\9\p #         v $                                   ;                                  ;
           ;;                 |:                               < <                 >\:] 9\g OO`#v_v  ; 2. we need to assign the strength;
           ;;                 $> :03g'a+\1\p 1+:00g- |                                |              ;    of each type of hand. add     ;
           ;;                 > 9'0g :!#v_:4`#v_ ::1- *2/ 03g\-03p 0"23456789FQRX"0^           $<\<  ;    1 point for first duplicate,  ;
           ;;                  ^       $<     <   p30 +/2*-1: +g0'9\ -\g30 /2*-1::   $<              ;    2 points for second, etc.     ;
           ;;       v  p10-1                     ,b' <                                               ;  p.2: the jack count is added    ;
           ;;;;;;;;   ; 3. sort hands ;;;;;;;;;;;; v this +/- controls the sort order ;;;;;;;;;;;;;;;;       to the most duplicated     ;
           ;;       > 01g:] :02p > 1- :1\G 102gG C1-] :02p v                                         ;                                  ;
           ;;             |      ^              <   |                                                ; 3. once we have strength as a    ;
           ;;             '  ;find candidate;   |:  <      <   ;swap candidate;                      ;    prefix, we can sort using     ;
           ;;             c                     >$ 101gG 01-01gg 08-u 102gG 101gP v                  ;    string comparison and         ;
           ;;       ^                   p10-1g10 Pg201 pg20-10 u8 pg10-10 gg20-10 <                  ;    selection sort                ;
           ;;;;;;;;;;;;;; ,  ; 4. accumulate winnings ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;                                  ;
           ;;             > 00g:] :1-:00p 01-\g :.\:. *+ v                                           ; 4. once the hands are sorted,    ;
           ;;                   |                                                                    ;    their position is their rank  ;
           ;;             ^                              <                                           ;                                  ;
           ;;                   >$ a,.a, @                                                           ;                                  ;
           ;;                                                                                        ;                                  ;
           ;;                                                                                        ;                                  ;